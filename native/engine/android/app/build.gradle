import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'
//apply plugin: 'com.huawei.hmf.modulediscoverer'

RES_PATH = RES_PATH.replace("\\", "/")
COCOS_ENGINE_PATH = COCOS_ENGINE_PATH.replace("\\", "/")

buildDir = "${RES_PATH}/proj/build/${project.name ==~ /^[_a-zA-Z0-9-]+$/ ? project.name : 'CocosGame'}"
android {
    compileSdkVersion PROP_COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion PROP_BUILD_TOOLS_VERSION
    ndkPath PROP_NDK_PATH
    namespace APPLICATION_ID

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                targets "cocos"
                arguments "-DRES_DIR=${RES_PATH}", "-DANDROID_STL=c++_static", "-DANDROID_TOOLCHAIN=clang", "-DANDROID_ARM_NEON=TRUE"
            }
            ndk { abiFilters PROP_APP_ABI.split(':') }
        }
    }

    sourceSets.main {
        java.srcDirs "../src", "src"
        res.srcDirs "../res", 'res', "${RES_PATH}/proj/res"
        jniLibs.srcDirs "../libs", 'libs'
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "${RES_PATH}/data"
        jniLibs {
            // Vulkan validation layer
            // srcDir "${android.ndkDirectory}/sources/third_party/vulkan/src/build-android/jniLibs"
        }
    }

    externalNativeBuild {
        cmake {
            version "3.22.1"
            path "../CMakeLists.txt"
            buildStagingDirectory "${RES_PATH}/proj/build"
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty("RELEASE_STORE_FILE") && !RELEASE_STORE_FILE.isEmpty()) {
                storeFile file('/Users/test/Documents/cocos/hunter/key/cocoskey')
                storePassword 'cocos2023'
                keyAlias 'key0'
                keyPassword 'cocos2023'
            }
        }

        debug {
            storeFile file('/Users/test/Documents/cocos/hunter/key/cocoskey')
            storePassword 'cocos2023'
            keyAlias 'key0'
            keyPassword 'cocos2023'
        }
    }


    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
                        
            externalNativeBuild {
                cmake {
                    // switch HIDE_SYMBOLS to OFF to skip compilation flag `-fvisibility=hidden`
                    arguments "-DHIDE_SYMBOLS=ON"
                }
            }

            if (!Boolean.parseBoolean(PROP_IS_DEBUG)) {
                getIsDefault().set(true)
            }

        }

        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            // resValue  "string", "app_name", "${PROP_APP_NAME}-dbg"
            // applicationIdSuffix ".debug"
        }
    }
}

dependencies {
    implementation fileTree(dir: '../libs', include: ['*.jar','*.aar'])
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation fileTree(dir: "${COCOS_ENGINE_PATH}/cocos/platform/android/java/libs", include: ['*.jar'])
    implementation project(':libservice')
    implementation project(':libcocos')
    if (Boolean.parseBoolean(PROP_ENABLE_INPUTSDK)) {
        implementation 'com.google.android.libraries.play.games:inputmapping:1.1.0-beta'
        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.10"
    }

    // android原生依赖
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "pl.droidsonroids.gif:android-gif-drawable:1.2.24"
//
    // mmsdk-common：游戏多媒体服务基础SDK，必须集成
    implementation 'com.huawei.game.midplatform:mmsdk-common:1.10.2.300'
    // mmsdk-gmme：游戏多媒体服务实时语音模块，实现语音通话功能时集成
    implementation 'com.huawei.game.midplatform:mmsdk-gmme:1.10.2.300'
    // mmsdk-gmme-im：游戏多媒体服务文本消息模块，实现文本消息功能时集成
    implementation 'com.huawei.game.midplatform:mmsdk-gmme-im:1.10.2.300'
    // mmsdk-gmme-audio-msg：游戏多媒体服务语音消息模块，实现语音消息功能时集成
    implementation 'com.huawei.game.midplatform:mmsdk-gmme-audio-msg:1.10.2.300'

    implementation 'com.huawei.game.moment.sdk:gamemomentsdk:13.5.1.301'
    implementation 'com.huawei.hms:hwid:6.11.0.300'
    implementation 'com.huawei.hms:game:6.10.0.300'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'

}
